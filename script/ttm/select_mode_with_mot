;
; select [normal] or [mot]
;
; require
;	dir_mot	# ${renesas-bsp-rom-writer}\${dir_mot}}\${mot}
;	mot
;
; return
;	dir_mot
;	mot
;	cpld_mot
;	mode	# "normal" or "mot"
;

;
; load config
;	dir_mot		(from mot file)
;	mot		(from mot file)
;	cpld_mot	(from mot file if exist)
;
sprintf2 __tmp "%s\%s\config\mot" dir_top board
include __tmp

;
; create explanation message
;
groupmatchstr1=""
sprintf2 groupmatchstr2 "Linux   : ${renesas_bsp_rom_writer}\%s\%s" dir_mot mot
sprintf2 groupmatchstr3 "Windows : ${renesas_bsp_rom_writer}\%s" board
strjoin __copy_from "\n" 3
strspecial __copy_from

groupmatchstr1="[normal]"
groupmatchstr2="Need manual Dip-Switch settings to write ROM."
groupmatchstr3="You need to select normal mode"
groupmatchstr4="if your current board doesn't have U-boot."
groupmatchstr5=""
strjoin __explanation "\n" 5

groupmatchstr1=__explanation
groupmatchstr2="[mot]"
groupmatchstr3="No or a few Dip-Switch settings is needed."
groupmatchstr4="But you need mot file. Copy it from Linux user."
sprintf2 groupmatchstr5 "see ${renesas_bsp_rom_writer}\%s\windows\README" board
groupmatchstr6=__copy_from
strjoin __explanation "\n" 6
strspecial __explanation

;
; ask
;
strdim __list_mode 2
__list_mode[0] = "normal"
__list_mode[1] = "mot"

listbox __explanation "Mode Select" __list_mode 1
if result<0 end

mode = __list_mode[result]

;
; return if normal
; check mot if mot
;
strcompare mode "normal"
if result!=0 then

  ; check mot file
  sprintf2 __mot_file "%s\%s\%s" dir_top board mot
  filesearch __mot_file
  if result=0 then
     groupmatchstr1="You don't have mot file. Copy it from Linux user."
     groupmatchstr2=__copy_from
     strjoin __txt "\n" 2
     strspecial __txt
     messagebox __txt "mot file error"
     end
  endif
endif
